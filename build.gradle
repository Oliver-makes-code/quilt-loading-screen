//file:noinspection GroovyAssignabilityCheck
plugins {
  id "org.quiltmc.loom" version "0.10.7ced5b7"
  id "com.modrinth.minotaur" version "1.2.+"
  id "org.cadixdev.licenser" version "0.6.+"
  id "maven-publish"
}

boolean is_release = false
String mod_version = loom.modVersion
String minecraft_version = "1.17.1"

// https://maven.quiltmc.org/service/rest/repository/browse/release/org/quiltmc/quilt-mappings/
String quilt_mappings = "15"

// https://maven.quiltmc.org/service/rest/repository/browse/snapshot/org/quiltmc/quilt-loader/
String quilt_version = "0.14.0-SNAPSHOT"

// https://maven.quiltmc.org/service/rest/repository/browse/snapshot/org/quiltmc/qsl/core/resource-loader/
String res_loader_version = "1.0.0-SNAPSHOT"

// https://modrinth.com/mod/modmenu/versions
// String modmenu_version = "2.0.14"

archivesBaseName = "quilt-loading-screen"
version = mod_version + "+" + minecraft_version + (!is_release ? "-SNAPSHOT" : "")

repositories {
  // maven { url "https://maven.waffle.coffee/snapshots/" }
  maven { url "https://maven.quiltmc.org/repository/release/" }
  maven { url "https://maven.quiltmc.org/repository/snapshot/" }
  // maven { url "https://maven.terraformersmc.com" }
}

dependencies {
  minecraft "com.mojang:minecraft:${minecraft_version}"
  mappings "org.quiltmc:quilt-mappings:${minecraft_version}+build.${quilt_mappings}:v2"
  modImplementation "org.quiltmc:quilt-loader:${quilt_version}"
  include modImplementation("org.quiltmc.qsl.core:resource-loader:${res_loader_version}")

  // modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
}

// Comment this line out if you have good internet
idea { module { downloadJavadoc = false } }

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

jar { from("LICENSE") }

task modrinth(type: com.modrinth.minotaur.TaskModrinthUpload) {
  onlyIf { System.getenv("MODRINTH") }
  token = System.getenv("MODRINTH")
  projectId = "VPU6VYVP"
  versionNumber = mod_version
  versionType = "RELEASE"
  uploadFile = remapJar
  addGameVersion minecraft_version
  addLoader "fabric"
}

java { withSourcesJar() }

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'coffee.waffle'
      artifactId = archivesBaseName
      version = version
      artifact(remapJar) { builtBy remapJar }
      artifact(sourcesJar) { builtBy remapSourcesJar }
    }
  }
  repositories {
    maven {
      url = System.getenv().MAVEN_URL
      credentials {
        username = System.getenv().MAVEN_USER
        password = System.getenv().MAVEN_PASS
      }
      authentication { basic(BasicAuthentication) }
    }
  }
}
