//file:noinspection GroovyAssignabilityCheck
plugins {
  id "org.quiltmc.loom" version "0.9.local"
  id "com.modrinth.minotaur" version "1.2.+"
  id "org.cadixdev.licenser" version "0.6.+"
  id "maven-publish"
}

boolean is_release = false
String mod_version = loom.modVersion
String minecraft_version = "1.17.1"
String quilt_mappings = "11"        // https://maven.quiltmc.org/service/rest/repository/browse/release/org/quiltmc/quilt-mappings/
String quilt_version = "0.13.2"     // https://maven.quiltmc.org/service/rest/repository/browse/release/org/quiltmc/quilt-loader/
String res_loader_version = "1.0.0" // https://maven.quiltmc.org/service/rest/repository/browse/snapshot/org/quiltmc/qsl/core/resource-loader
// String modmenu_version = "2.0.14"     // https://modrinth.com/mod/modmenu/versions

archivesBaseName = "quilt-loading-screen"
version = mod_version + "+" + minecraft_version + (!is_release ? "-SNAPSHOT" : "")

repositories {
  maven { url "https://maven.waffle.coffee" }
  maven { url "https://maven.quiltmc.org/repository/release/" }
  maven { url "https://maven.quiltmc.org/repository/snapshot/" }
  // maven { url "https://maven.terraformersmc.com" }
}

dependencies {
  minecraft "com.mojang:minecraft:${minecraft_version}"
  mappings "org.quiltmc:quilt-mappings:${minecraft_version}+build.${quilt_mappings}:v2"
  modImplementation "org.quiltmc:quilt-loader:${quilt_version}"
  include modImplementation("org.quiltmc.qsl.core:resource-loader:${res_loader_version}")

  // modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
}

// Comment this line out if you have good internet
idea { module { downloadJavadoc = false } }

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

jar { from("LICENSE") }

task modrinth(type: com.modrinth.minotaur.TaskModrinthUpload) {
  onlyIf { System.getenv("MODRINTH") }
  token = System.getenv("MODRINTH")
  projectId = "VPU6VYVP"
  versionNumber = mod_version
  versionType = "RELEASE"
  uploadFile = remapJar
  addGameVersion minecraft_version
  addLoader "fabric"
}

java { withSourcesJar() }

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'coffee.waffle'
      artifactId = archivesBaseName
      version = version
      artifact(remapJar) { builtBy remapJar }
      artifact(sourcesJar) { builtBy remapSourcesJar }
    }
  }
  repositories {
    maven {
      name = "emmavenReleases"
      url = "https://maven.waffle.coffee/releases"
      credentials {
        username = System.getenv().MAVEN_USER
        password = System.getenv().MAVEN_PASS
      }
      authentication { basic(BasicAuthentication) }
    }
    maven {
      name = "emmavenSnapshots"
      url = "https://maven.waffle.coffee/snapshots"
      credentials {
        username = System.getenv().MAVEN_USER
        password = System.getenv().MAVEN_PASS
      }
      authentication { basic(BasicAuthentication) }
    }
  }
}
